# Version Compute Capability from which AS has been compiled is remembered
set(AS_COMPUTE_CAPABILITIES "@AS_CUDA_CC@")

set(AS_CUDA_VERSION "@CUDA_VERSION_STRING@")
set(AS_USE_CUBLAS   "@HAVE_CUBLAS@")
set(AS_USE_CUFFT    "@HAVE_CUFFT@")
set(AS_USE_NVCUVID  "@HAVE_NVCUVID@")

set(AS_CUDNN_VERSION    "@CUDNN_VERSION@")
set(AS_USE_CUDNN        "@HAVE_CUDNN@")

if(NOT CUDA_FOUND)
  find_host_package(CUDA ${AS_CUDA_VERSION} EXACT REQUIRED)
else()
  if(NOT CUDA_VERSION_STRING VERSION_EQUAL AS_CUDA_VERSION)
    message(FATAL_ERROR "AS static library was compiled with CUDA ${AS_CUDA_VERSION} support. Please, use the same version or rebuild AS with CUDA ${CUDA_VERSION_STRING}")
  endif()
endif()

set(AS_CUDA_LIBS_ABSPATH ${CUDA_LIBRARIES})

if(CUDA_VERSION VERSION_LESS "5.5")
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_npp_LIBRARY})
else()
  find_cuda_helper_libs(nppc)
  find_cuda_helper_libs(nppi)
  find_cuda_helper_libs(npps)
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_nppc_LIBRARY} ${CUDA_nppi_LIBRARY} ${CUDA_npps_LIBRARY})
endif()

if(AS_USE_CUBLAS)
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_CUBLAS_LIBRARIES})
endif()

if(AS_USE_CUFFT)
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_CUFFT_LIBRARIES})
endif()

if(AS_USE_NVCUVID)
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_nvcuvid_LIBRARIES})
endif()

if(WIN32)
  list(APPEND AS_CUDA_LIBS_ABSPATH ${CUDA_nvcuvenc_LIBRARIES})
endif()

set(AS_CUDA_LIBS_RELPATH "")
foreach(l ${AS_CUDA_LIBS_ABSPATH})
  get_filename_component(_tmp ${l} PATH)
  if(NOT ${_tmp} MATCHES "-Wl.*")
    list(APPEND AS_CUDA_LIBS_RELPATH ${_tmp})
  endif()
endforeach()

list(REMOVE_DUPLICATES AS_CUDA_LIBS_RELPATH)
link_directories(${AS_CUDA_LIBS_RELPATH})
