# ===================================================================================
#  The AS CMake configuration file
#
#             ** File generated automatically, do not modify **
#
#  Usage from an external project:
#    In your CMakeLists.txt, add these lines:
#
#    find_package(AS REQUIRED)
#    include_directories(${AS_INCLUDE_DIRS}) # Not needed for CMake >= 2.8.11
#    target_link_libraries(MY_TARGET_NAME ${AS_LIBS})
#
#    Or you can search for specific AS modules:
#
#    find_package(AS REQUIRED core videoio)
#
#    You can also mark AS components as optional:

#    find_package(AS REQUIRED core OPTIONAL_COMPONENTS viz)
#
#    If the module is found then AS_<MODULE>_FOUND is set to TRUE.
#
#    This file will define the following variables:
#      - AS_LIBS                     : The list of all imported targets for AS modules.
#      - AS_INCLUDE_DIRS             : The AS include directories.
#      - AS_COMPUTE_CAPABILITIES     : The version of compute capability.
#      - AS_ANDROID_NATIVE_API_LEVEL : Minimum required level of Android API.
#      - AS_VERSION                  : The version of this AS build: "@AS_VERSION_PLAIN@"
#      - AS_VERSION_MAJOR            : Major version part of AS_VERSION: "@AS_VERSION_MAJOR@"
#      - AS_VERSION_MINOR            : Minor version part of AS_VERSION: "@AS_VERSION_MINOR@"
#      - AS_VERSION_PATCH            : Patch version part of AS_VERSION: "@AS_VERSION_PATCH@"
#      - AS_VERSION_STATUS           : Development status of this build: "@AS_VERSION_STATUS@"
#
#    Advanced variables:
#      - AS_SHARED                   : Use AS as shared library
#      - AS_INSTALL_PATH             : AS location
#      - AS_LIB_COMPONENTS           : Present AS modules list
#      - AS_USE_MANGLED_PATHS        : Mangled AS path flag
#
#    Deprecated variables:
#      - AS_VERSION_TWEAK            : Always "0"
#
# ===================================================================================

# ======================================================
#  Version variables:
# ======================================================
SET(AS_VERSION @AS_VERSION_PLAIN@)
SET(AS_VERSION_MAJOR  @AS_VERSION_MAJOR@)
SET(AS_VERSION_MINOR  @AS_VERSION_MINOR@)
SET(AS_VERSION_PATCH  @AS_VERSION_PATCH@)
SET(AS_VERSION_TWEAK  0)
SET(AS_VERSION_STATUS "@AS_VERSION_STATUS@")

include(FindPackageHandleStandardArgs)

if(NOT CMAKE_VERSION VERSION_LESS 2.8.8
    AND AS_FIND_COMPONENTS  # prevent excessive output
)
  # HANDLE_COMPONENTS was introduced in CMake 2.8.8
  list(APPEND _AS_FPHSA_ARGS HANDLE_COMPONENTS)
  # The missing components will be handled by the FindPackageHandleStandardArgs
  # module.
  set(_AS_HANDLE_COMPONENTS_MANUALLY FALSE)
else()
  # The missing components will be handled by this config.
  set(_AS_HANDLE_COMPONENTS_MANUALLY TRUE)
endif()

# Extract directory name from full path of the file currently being processed.
# Note that CMake 2.8.3 introduced CMAKE_CURRENT_LIST_DIR. We reimplement it
# for older versions of CMake to support these as well.
if(CMAKE_VERSION VERSION_LESS "2.8.3")
  get_filename_component(CMAKE_CURRENT_LIST_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
endif()

# Extract the directory where *this* file has been installed (determined at cmake run-time)
# Get the absolute path with no ../.. relative marks, to eliminate implicit linker warnings
get_filename_component(AS_CONFIG_PATH "${CMAKE_CURRENT_LIST_DIR}" REALPATH)
get_filename_component(AS_INSTALL_PATH "${AS_CONFIG_PATH}/@AS_INSTALL_PATH_RELATIVE_CONFIGCMAKE@" REALPATH)

# Search packages for host system instead of packages for target system.
# in case of cross compilation this macro should be defined by toolchain file
if(NOT COMMAND find_host_package)
    macro(find_host_package)
        find_package(${ARGN})
    endmacro()
endif()
if(NOT COMMAND find_host_program)
    macro(find_host_program)
        find_program(${ARGN})
    endmacro()
endif()


@CUDA_CONFIGCMAKE@
@ANDROID_CONFIGCMAKE@

@IPPICV_CONFIGCMAKE@
@IPPIW_CONFIGCMAKE@

# Some additional settings are required if AS is built as static libs
set(AS_SHARED @BUILD_SHARED_LIBS@)

# Enables mangled install paths, that help with side by side installs
set(AS_USE_MANGLED_PATHS @AS_USE_MANGLED_PATHS_CONFIGCMAKE@)

set(AS_LIB_COMPONENTS @AS_MODULES_CONFIGCMAKE@)
set(__AS_INCLUDE_DIRS @AS_INCLUDE_DIRS_CONFIGCMAKE@)

set(AS_INCLUDE_DIRS "")
foreach(d ${__AS_INCLUDE_DIRS})
  get_filename_component(__d "${d}" REALPATH)
  if(NOT EXISTS "${__d}")
    if(NOT AS_FIND_QUIETLY)
      message(WARNING "AS: Include directory doesn't exist: '${d}'. AS installation may be broken. Skip...")
    endif()
  else()
    list(APPEND AS_INCLUDE_DIRS "${__d}")
  endif()
endforeach()
unset(__d)


if(NOT TARGET as_core)
  include(${CMAKE_CURRENT_LIST_DIR}/ASModules${AS_MODULES_SUFFIX}.cmake)
endif()

if(NOT CMAKE_VERSION VERSION_LESS "2.8.11")
  # Target property INTERFACE_INCLUDE_DIRECTORIES available since 2.8.11:
  # * http://www.cmake.org/cmake/help/v2.8.11/cmake.html#prop_tgt:INTERFACE_INCLUDE_DIRECTORIES
  foreach(__component ${AS_LIB_COMPONENTS})
    if(TARGET ${__component})
      set_target_properties(
          ${__component}
          PROPERTIES
          INTERFACE_INCLUDE_DIRECTORIES "${AS_INCLUDE_DIRS}"
      )
    endif()
  endforeach()
endif()


if(NOT DEFINED AS_MAP_IMPORTED_CONFIG)
  if(CMAKE_GENERATOR MATCHES "Visual Studio" OR MSVC)
    # AS supports Debug and Release builds only.
    # But MSVS has 'RelWithDebInfo' 'MinSizeRel' configurations for applications.
    # By default CMake maps these configuration on the first available (Debug) which is wrong.
    # Non-Debug build of Application can't be used with AS Debug build (ABI miasatch problem)
    # Add mapping of RelWithDebInfo and MinSizeRel to Release here
    set(AS_MAP_IMPORTED_CONFIG "RELWITHDEBINFO=!Release;MINSIZEREL=!Release")
  endif()
endif()
set(__remap_warnings "")
macro(as_map_imported_config target)
  if(DEFINED AS_MAP_IMPORTED_CONFIG) # list, "RELWITHDEBINFO=Release;MINSIZEREL=Release"
    get_target_property(__available_configurations ${target} IMPORTED_CONFIGURATIONS)
    foreach(remap ${AS_MAP_IMPORTED_CONFIG})
      if(remap MATCHES "^(.+)=(!?)([^!]+)$")
        set(__remap_config "${CMAKE_MATCH_1}")
        set(__final_config "${CMAKE_MATCH_3}")
        set(__force_flag "${CMAKE_MATCH_2}")
        string(TOUPPER "${__final_config}" __final_config_upper)
        string(TOUPPER "${__remap_config}" __remap_config_upper)
        if(";${__available_configurations};" MATCHES ";${__remap_config_upper};" AND NOT "${__force_flag}" STREQUAL "!")
          # configuration already exists, skip remap
          set(__remap_warnings "${__remap_warnings}... Configuration already exists ${__remap_config} (skip mapping ${__remap_config} => ${__final_config}) (available configurations: ${__available_configurations})\n")
          continue()
        endif()
        if(__available_configurations AND NOT ";${__available_configurations};" MATCHES ";${__final_config_upper};")
          # skip, configuration is not available
          if(NOT "${__force_flag}" STREQUAL "!")
            set(__remap_warnings "${__remap_warnings}... Configuration is not available '${__final_config}' for ${target}, build may fail (available configurations: ${__available_configurations})\n")
          endif()
        endif()
        set_target_properties(${target} PROPERTIES
            MAP_IMPORTED_CONFIG_${__remap_config} "${__final_config}"
        )
      else()
        message(WARNING "Invalid entry of AS_MAP_IMPORTED_CONFIG: '${remap}' (${AS_MAP_IMPORTED_CONFIG})")
      endif()
    endforeach()
  endif()
endmacro()


# ==============================================================
#  Form list of modules (components) to find
# ==============================================================
if(NOT AS_FIND_COMPONENTS)
  set(AS_FIND_COMPONENTS ${AS_LIB_COMPONENTS})
  list(REMOVE_ITEM AS_FIND_COMPONENTS as_java)
  if(GTest_FOUND OR GTEST_FOUND)
    list(REMOVE_ITEM AS_FIND_COMPONENTS as_ts)
  endif()
endif()

set(AS_WORLD_COMPONENTS @AS_WORLD_MODULES@)

# expand short module names and see if requested components exist
foreach(__cvcomponent ${AS_FIND_COMPONENTS})
  # Store the name of the original component so we can set the
  # AS_<component>_FOUND variable which can be checked by the user.
  set (__original_cvcomponent ${__cvcomponent})
  if(NOT __cvcomponent MATCHES "^as_")
    set(__cvcomponent as_${__cvcomponent})
  endif()
  list(FIND AS_LIB_COMPONENTS ${__cvcomponent} __cvcomponentIdx)
  if(__cvcomponentIdx LESS 0)
    if(_AS_HANDLE_COMPONENTS_MANUALLY)
      # Either the component is required or the user did not set any components at
      # all. In the latter case, the AS_FIND_REQUIRED_<component> variable
      # will not be defined since it is not set by this config. So let's assume
      # the implicitly set components are always required.
      if(NOT DEFINED AS_FIND_REQUIRED_${__original_cvcomponent} OR
          AS_FIND_REQUIRED_${__original_cvcomponent})
        message(FATAL_ERROR "${__cvcomponent} is required but was not found")
      elseif(NOT AS_FIND_QUIETLY)
        # The component was marked as optional using OPTIONAL_COMPONENTS
        message(WARNING "Optional component ${__cvcomponent} was not found")
      endif()
    endif(_AS_HANDLE_COMPONENTS_MANUALLY)
    #indicate that module is NOT found
    string(TOUPPER "${__cvcomponent}" __cvcomponentUP)
    set(${__cvcomponentUP}_FOUND "${__cvcomponentUP}_FOUND-NOTFOUND")
    set(AS_${__original_cvcomponent}_FOUND FALSE)
  else()
    # Not using list(APPEND) here, because AS_LIBS may not exist yet.
    # Also not clearing AS_LIBS anywhere, so that multiple calls
    # to find_package(AS) with different component lists add up.
    set(AS_LIBS ${AS_LIBS} "${__cvcomponent}")
    #indicate that module is found
    string(TOUPPER "${__cvcomponent}" __cvcomponentUP)
    set(${__cvcomponentUP}_FOUND 1)
    set(AS_${__original_cvcomponent}_FOUND TRUE)
  endif()
  if(AS_SHARED AND ";${AS_WORLD_COMPONENTS};" MATCHES ";${__cvcomponent};" AND NOT TARGET ${__cvcomponent})
    get_target_property(__implib_dbg as_world IMPORTED_IMPLIB_DEBUG)
    get_target_property(__implib_release as_world  IMPORTED_IMPLIB_RELEASE)
    get_target_property(__location_dbg as_world IMPORTED_LOCATION_DEBUG)
    get_target_property(__location_release as_world  IMPORTED_LOCATION_RELEASE)
    get_target_property(__include_dir as_world INTERFACE_INCLUDE_DIRECTORIES)
    add_library(${__cvcomponent} SHARED IMPORTED)
    set_target_properties(${__cvcomponent} PROPERTIES INTERFACE_INCLUDE_DIRECTORIES "${__include_dir}")
    if(__location_dbg)
      set_property(TARGET ${__cvcomponent} APPEND PROPERTY IMPORTED_CONFIGURATIONS DEBUG)
      set_target_properties(${__cvcomponent} PROPERTIES
        IMPORTED_IMPLIB_DEBUG "${__implib_dbg}"
        IMPORTED_LINK_INTERFACE_LIBRARIES_DEBUG ""
        IMPORTED_LOCATION_DEBUG "${__location_dbg}"
      )
    endif()
    if(__location_release)
      set_property(TARGET ${__cvcomponent} APPEND PROPERTY IMPORTED_CONFIGURATIONS RELEASE)
      set_target_properties(${__cvcomponent} PROPERTIES
        IMPORTED_IMPLIB_RELEASE "${__implib_release}"
        IMPORTED_LINK_INTERFACE_LIBRARIES_RELEASE ""
        IMPORTED_LOCATION_RELEASE "${__location_release}"
      )
    endif()
  endif()
  if(TARGET ${__cvcomponent})
    as_map_imported_config(${__cvcomponent})
  endif()
endforeach()

if(__remap_warnings AND NOT AS_FIND_QUIETLY)
  message("AS: configurations remap warnings:\n${__remap_warnings}AS: Check variable AS_MAP_IMPORTED_CONFIG=${AS_MAP_IMPORTED_CONFIG}")
endif()

# ==============================================================
# Compatibility stuff
# ==============================================================
set(AS_LIBRARIES ${AS_LIBS})

# Require C++11 features for AS modules
if(CMAKE_VERSION VERSION_LESS "3.1")
  if(NOT AS_FIND_QUIETLY AND NOT AS_HIDE_WARNING_COMPILE_FEATURES)
    message(STATUS "AS: CMake version is low (${CMAKE_VERSION}, required 3.1+). Can't enable C++11 features: https://github.com/as/as/issues/13000")
  endif()
else()
  set(__target as_core)
  if(TARGET as_world)
    set(__target as_world)
  endif()
  set(__compile_features cxx_std_11)  # CMake 3.8+
  if(DEFINED AS_COMPILE_FEATURES)
    set(__compile_features ${AS_COMPILE_FEATURES})  # custom override
  elseif(CMAKE_VERSION VERSION_LESS "3.8")
    set(__compile_features cxx_auto_type cxx_rvalue_references cxx_lambdas)
  endif()
  if(__compile_features)
    # Simulate exported result of target_compile_features(as_core PUBLIC ...)
    set_target_properties(${__target} PROPERTIES
        INTERFACE_COMPILE_FEATURES "${__compile_features}"
    )
  endif()
  unset(__target)
  unset(__compile_features)
endif()

#
# Some macros for samples
#
macro(as_check_dependencies)
  set(AS_DEPENDENCIES_FOUND TRUE)
  foreach(d ${ARGN})
    if(NOT TARGET ${d})
      message(WARNING "AS: Can't resolve dependency: ${d}")
      set(AS_DEPENDENCIES_FOUND FALSE)
      break()
    endif()
  endforeach()
endmacro()

# adds include directories in such way that directories from the AS source tree go first
function(as_include_directories)
  set(__add_before "")
  file(TO_CMAKE_PATH "${AS_INSTALL_PATH}" __baseDir)
  foreach(dir ${ARGN})
    get_filename_component(__abs_dir "${dir}" ABSOLUTE)
    if("${__abs_dir}" MATCHES "^${__baseDir}")
      list(APPEND __add_before "${dir}")
    else()
      include_directories(AFTER SYSTEM "${dir}")
    endif()
  endforeach()
  include_directories(BEFORE ${__add_before})
endfunction()

macro(as_include_modules)
  include_directories(BEFORE "${AS_INCLUDE_DIRS}")
endmacro()

macro(as_include_modules_recurse)
  include_directories(BEFORE "${AS_INCLUDE_DIRS}")
endmacro()

macro(as_target_link_libraries)
  target_link_libraries(${ARGN})
endmacro()

# remove all matching elements from the list
macro(as_list_filterout lst regex)
  foreach(item ${${lst}})
    if(item MATCHES "${regex}")
      list(REMOVE_ITEM ${lst} "${item}")
    endif()
  endforeach()
endmacro()

# We do not actually need REQUIRED_VARS to be checked for. Just use the
# installation directory for the status.
find_package_handle_standard_args(AS REQUIRED_VARS AS_INSTALL_PATH
                                  VERSION_VAR AS_VERSION ${_AS_FPHSA_ARGS})
