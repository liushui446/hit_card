# ===================================================================================
#  The AS CMake configuration file
#
#             ** File generated automatically, do not modify **
#
#  Usage from an external project:
#    In your CMakeLists.txt, add these lines:
#
#    FIND_PACKAGE(AS REQUIRED)
#    TARGET_LINK_LIBRARIES(MY_TARGET_NAME ${AS_LIBS})
#
#    Or you can search for specific AS modules:
#
#    FIND_PACKAGE(AS REQUIRED core imgcodecs)
#
#    If the module is found then AS_<MODULE>_FOUND is set to TRUE.
#
#    This file will define the following variables:
#      - AS_LIBS                     : The list of libraries to link against.
#      - AS_INCLUDE_DIRS             : The AS include directories.
#      - AS_COMPUTE_CAPABILITIES     : The version of compute capability
#      - AS_VERSION                  : The version of this AS build: "@AS_VERSION_PLAIN@"
#      - AS_VERSION_MAJOR            : Major version part of AS_VERSION: "@AS_VERSION_MAJOR@"
#      - AS_VERSION_MINOR            : Minor version part of AS_VERSION: "@AS_VERSION_MINOR@"
#      - AS_VERSION_PATCH            : Patch version part of AS_VERSION: "@AS_VERSION_PATCH@"
#      - AS_VERSION_STATUS           : Development status of this build: "@AS_VERSION_STATUS@"
#
#    Advanced variables:
#      - AS_SHARED
#
# ===================================================================================
#
#    Windows pack specific options:
#      - AS_STATIC
#      - AS_CUDA

if(CMAKE_VERSION VERSION_GREATER 2.6)
  get_property(AS_LANGUAGES GLOBAL PROPERTY ENABLED_LANGUAGES)
  if(NOT ";${AS_LANGUAGES};" MATCHES ";CXX;")
    enable_language(CXX)
  endif()
endif()

if(NOT DEFINED AS_STATIC)
  # look for global setting
  if(NOT DEFINED BUILD_SHARED_LIBS OR BUILD_SHARED_LIBS)
    set(AS_STATIC OFF)
  else()
    set(AS_STATIC ON)
  endif()
endif()

if(NOT DEFINED AS_CUDA)
  # if user' app uses CUDA, then it probably wants CUDA-enabled AS binaries
  if(CUDA_FOUND)
    set(AS_CUDA ON)
  endif()
endif()

function(check_one_config RES)
  set(${RES} "" PARENT_SCOPE)
  if(NOT AS_RUNTIME OR NOT AS_ARCH)
    return()
  endif()
  set(candidates)
  if(AS_STATIC)
    list(APPEND candidates "${AS_ARCH}/${AS_RUNTIME}/staticlib")
  endif()
  if(AS_CUDA)
    list(APPEND candidates "gpu/${AS_ARCH}/${AS_RUNTIME}/lib")
  endif()
  if(AS_CUDA AND AS_STATIC)
    list(APPEND candidates "gpu/${AS_ARCH}/${AS_RUNTIME}/staticlib")
  endif()
  list(APPEND candidates "${AS_ARCH}/${AS_RUNTIME}/lib")
  foreach(c ${candidates})
    set(p "${AS_CONFIG_PATH}/${c}")
    if(EXISTS "${p}/ASConfig.cmake")
      set(${RES} "${p}" PARENT_SCOPE)
      return()
    endif()
  endforeach()
endfunction()

get_filename_component(AS_CONFIG_PATH "${CMAKE_CURRENT_LIST_FILE}" DIRECTORY)

if((NOT DEFINED CMAKE_SYSTEM_PROCESSOR OR CMAKE_SYSTEM_PROCESSOR STREQUAL "")
    AND NOT AS_SUPPRESS_MESSAGE_MISSING_CMAKE_SYSTEM_PROCESSOR)
  message(WARNING "AS: CMAKE_SYSTEM_PROCESSOR is not defined. Perhaps CMake toolchain is broken")
endif()
if(NOT DEFINED CMAKE_SIZEOF_VOID_P
    AND NOT AS_SUPPRESS_MESSAGE_MISSING_CMAKE_SIZEOF_VOID_P)
  message(WARNING "AS: CMAKE_SIZEOF_VOID_P is not defined. Perhaps CMake toolchain is broken")
endif()

if(DEFINED AS_ARCH AND DEFINED AS_RUNTIME)
  # custom overridden values
elseif(MSVC)
  # see Modules/CMakeGenericSystem.cmake
  if("${CMAKE_GENERATOR}" MATCHES "(Win64|IA64)")
    set(AS_ARCH "x64")
  elseif("${CMAKE_GENERATOR_PLATFORM}" MATCHES "ARM64")
    set(AS_ARCH "ARM64")
  elseif("${CMAKE_GENERATOR}" MATCHES "ARM")
    set(AS_ARCH "ARM")
  elseif("${CMAKE_SIZEOF_VOID_P}" STREQUAL "8")
    set(AS_ARCH "x64")
  else()
    set(AS_ARCH x86)
  endif()

  if(MSVC_VERSION EQUAL 1400)
    set(AS_RUNTIME vc8)
  elseif(MSVC_VERSION EQUAL 1500)
    set(AS_RUNTIME vc9)
  elseif(MSVC_VERSION EQUAL 1600)
    set(AS_RUNTIME vc10)
  elseif(MSVC_VERSION EQUAL 1700)
    set(AS_RUNTIME vc11)
  elseif(MSVC_VERSION EQUAL 1800)
    set(AS_RUNTIME vc12)
  elseif(MSVC_VERSION EQUAL 1900)
    set(AS_RUNTIME vc14)
  elseif(MSVC_VERSION MATCHES "^191[0-9]$")
    set(AS_RUNTIME vc15)
    check_one_config(has_VS2017)
    if(NOT has_VS2017)
      set(AS_RUNTIME vc14) # selecting previous compatible runtime version
    endif()
  elseif(MSVC_VERSION MATCHES "^192[0-9]$")
    set(AS_RUNTIME vc16)
    check_one_config(has_VS2019)
    if(NOT has_VS2019)
      set(AS_RUNTIME vc15) # selecting previous compatible runtime version
      check_one_config(has_VS2017)
      if(NOT has_VS2017)
        set(AS_RUNTIME vc14) # selecting previous compatible runtime version
      endif()
    endif()
  endif()
elseif(MINGW)
  set(AS_RUNTIME mingw)

  if(CMAKE_SYSTEM_PROCESSOR MATCHES "amd64.*|x86_64.*|AMD64.*")
    set(AS_ARCH x64)
  else()
    set(AS_ARCH x86)
  endif()
endif()

check_one_config(AS_LIB_PATH)

if(NOT AS_FIND_QUIETLY)
  message(STATUS "AS ARCH: ${AS_ARCH}")
  message(STATUS "AS RUNTIME: ${AS_RUNTIME}")
  message(STATUS "AS STATIC: ${AS_STATIC}")
endif()

if(AS_LIB_PATH AND EXISTS "${AS_LIB_PATH}/ASConfig.cmake")
  include("${AS_LIB_PATH}/ASConfig.cmake")

  if(NOT AS_FIND_QUIETLY)
    message(STATUS "Found AS ${AS_VERSION} in ${AS_LIB_PATH}")
    if(NOT AS_LIB_PATH MATCHES "/staticlib")
      get_filename_component(_AS_LIB_PATH "${AS_LIB_PATH}/../bin" ABSOLUTE)
      file(TO_NATIVE_PATH "${_AS_LIB_PATH}" _AS_LIB_PATH)
      message(STATUS "You might need to add ${_AS_LIB_PATH} to your PATH to be able to run your applications.")
      if(AS_LIB_PATH MATCHES "/gpu/")
        string(REPLACE "\\gpu" "" _AS_LIB_PATH2 "${_AS_LIB_PATH}")
        message(STATUS "GPU support is enabled so you might also need ${_AS_LIB_PATH2} in your PATH (it must go after the ${_AS_LIB_PATH}).")
      endif()
    endif()
  endif()
else()
  if(NOT AS_FIND_QUIETLY)
    message(WARNING
"Found AS Windows Pack but it has no binaries compatible with your configuration.
You should manually point CMake variable AS_DIR to your build of AS library."
    )
  endif()
  set(AS_FOUND FALSE)
endif()
